apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
  compileSdkVersion project.ext.compileSdkVersion
  buildToolsVersion project.ext.buildToolsVersion
  ndkVersion        project.ext.ndkVersion

  compileOptions {
    sourceCompatibility project.ext.javaVersion
    targetCompatibility project.ext.javaVersion
  }

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    applicationId "com.github.warren_bank.nodejs_frontend"
    versionName project.ext.releaseVersion
    versionCode project.ext.releaseVersionCode

    archivesBaseName += "-$versionName"

    externalNativeBuild {
      cmake {
        cppFlags ""
        arguments "-DANDROID_STL=c++_shared"
      }
    }

    ndk {
      abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
      abiFilters.clear()
    }
  }

  lintOptions {
    disable 'MissingTranslation'
    abortOnError true
  }

  buildTypes {
    release {
      shrinkResources true
      minifyEnabled true
      proguardFiles = [
        "proguard-rules.txt",
        getDefaultProguardFile('proguard-android.txt')
      ]
    }
    debug {
      jniDebuggable = true
    }
  }

  externalNativeBuild {
    cmake {
      version project.ext.cmakeVersion
      path "CMakeLists.txt"
    }
  }

  splits {
    abi {
      enable true
      reset()
      include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
      universalApk false
    }
  }
}

dependencies {
  implementation project(':folderpicker-library')
  implementation project(':folderpicker-support-library')

  compileOnly         'androidx.annotation:annotation:'           + project.ext.libSupportAnnotation           // ( 22 KB) https://mvnrepository.com/artifact/androidx.annotation/annotation?repo=google
  annotationProcessor 'androidx.annotation:annotation:'           + project.ext.libSupportAnnotation

  implementation 'androidx.appcompat:appcompat:'                  + project.ext.libSupportAppCompat            // (1.0 MB) https://mvnrepository.com/artifact/androidx.appcompat/appcompat?repo=google
  implementation 'androidx.coordinatorlayout:coordinatorlayout:'  + project.ext.libSupportCoordinatorLayout    // ( 44 KB) https://mvnrepository.com/artifact/androidx.coordinatorlayout/coordinatorlayout?repo=google
  implementation 'androidx.fragment:fragment:'                    + project.ext.libSupportFragment             // (221 KB) https://mvnrepository.com/artifact/androidx.fragment/fragment?repo=google
  implementation 'androidx.recyclerview:recyclerview:'            + project.ext.libSupportRecyclerView         // (349 KB) https://mvnrepository.com/artifact/androidx.recyclerview/recyclerview?repo=google
  implementation 'androidx.viewpager:viewpager:'                  + project.ext.libSupportViewPager            // ( 52 KB) https://mvnrepository.com/artifact/androidx.viewpager/viewpager?repo=google
  implementation 'com.google.android.material:material:'          + project.ext.libSupportMaterial             // (566 KB) https://mvnrepository.com/artifact/com.google.android.material/material?repo=google

  implementation 'com.google.code.gson:gson:'                     + project.ext.libGson                        // (235 KB) https://mvnrepository.com/artifact/com.google.code.gson/gson
}
